filename aaa 'C:\Documents and Settings\lyu\My Documents\yulili\course\survival data\ch4\2009\whas500.dat';
data whas500;
infile aaa;
input id age gender hr sysbp diasbp bmi cvd afb sho chf av3 miord mitype year admitdate disdate fdate los dstat lenfol fstat;
run;

/*Table 5.7  and Figure 5.1 on page 146 using the whas500 data.

We have created a macro for creating this table and the accompanying graph.  However, bmi can be treated as a discrete variable or not.  Because of this, we have included code so that the output will match exactly with the text.
*/
%macro table5_7(data, des_var, covlist);
  proc rank data = &data groups = 4 out = _temp_ ties = low;
      var &des_var;
      ranks new_&des_var;
  run;

  proc means data = _temp_ min max;
     class new_&des_var;
     var &des_var;
  ods output Summary = _temp_cut;
run;

  data _temp_a;
   set _temp_;
   &des_var.cat1 = (new_&des_var=1);
   &des_var.cat2 = (new_&des_var=2);
   &des_var.cat3 = (new_&des_var=3);
  run;

  proc phreg data = _temp_a;
     model lenfol*fstat(0) = &des_var.cat1 &des_var.cat2 &des_var.cat3 &covlist;
  ods output ParameterEstimates = _temp_parm;
  run;
data _temp_parma (keep = new_&des_var p);
  set _temp_parm;
  if _n_=1 then do;
  new_&des_var = 0;
  p = 0;
  output;
  new_&des_var = 1;
  p = estimate; 
  output;
  end;
  if _n_=2 then do;
  new_&des_var = 2;
  p = estimate; 
  output;
  end;
  if _n_=3 then do;
  new_&des_var = 3;
  p = estimate; 
  output;
  end;
if _n_<=3;
run;
data _temp_graph;
  merge _temp_cut  _temp_parma;
  by new_&des_var;
  x = (&des_var._min + &des_var._max)/2;
run;
proc print data = _temp_graph noobs label;
var x p;
format p f8.3;
format x f8.1;
label x="&des_var Quartile Midpoint" p='Coeff.';
run;
symbol i = join v = dot;
axis1 label=(a=90 'Estimated Log Hazard') minor=none;
axis2 label=("&des_var");
title "Estimated Coefficients for &des_var Quartiles";
proc gplot data = _temp_graph;
  plot p*x / vaxis=axis1 haxis=axis2;
run;
quit;
title;
%mend;

%table5_7(whas500, age, hr diasbp bmi gender chf);
%table5_7(whas500, hr, age diasbp bmi gender chf);
%table5_7(whas500, diasbp, age hr bmi gender chf);
%table5_7(whas500, bmi, age hr diasbp gender chf);
* code to make bmi match to text;
data part4b;
set whas500;
bmicat0 = (bmi le 23);
bmicat1 = (23 < bmi <= 26);
bmicat2 = (26< bmi <= 29);
bmicat3 = (bmi gt 29);
if bmicat0 = 1 then nbmi = 0;
if bmicat1 = 1 then nbmi = 1;
if bmicat2 = 1 then nbmi = 2;
if bmicat3 = 1 then nbmi = 3;
run;

proc freq data = part4b;
tables nbmi;
tables bmicat0 bmicat1 bmicat2 bmicat3;
run;

proc phreg data = part4b;
model lenfol*fstat(0) = bmicat1 bmicat2 bmicat3 age hr diasbp gender chf;
ods output ParameterEstimates = part4_parm;
run;
proc univariate data = whas500;
  var bmi;
  output out = part4_sum min=min q1=q1 median=median q3=q3 max=max;
run;
data part4_cut;
  set part4_sum;
  new_bmi = 0;
  x = (min+q1)/2;
  output;
  new_bmi = 1;
  x = (q1 + median)/2;
  output;
new_bmi = 2;
  x = (median + q3)/2;
  output;
new_bmi = 3;
  x = (q3+max)/2;
  output;
  keep new_bmi x;
run;

data part4_parma (keep = new_bmi p);
  set part4_parm;
  if _n_=1 then do;
  new_bmi = 0;
  p = 0;
  output;
  new_bmi = 1;
  p = estimate; 
  output;
  end;
  if _n_=2 then do;
  new_bmi = 2;
  p = estimate; 
  output;
  end;
  if _n_=3 then do;
  new_bmi = 3;
  p = estimate; 
  output;
  end;
if _n_<=3;
run;

data part4_graph;
  merge part4_cut part4_parma;
  by new_bmi;
  x = put(x, 8.1);
run;

proc print data = part4_graph noobs label;
var x p;
format p f8.3;
label x="BMI Quartile Midpoint" p='Coeff.';
run;
goptions reset=all;
symbol i = join v = dot;
axis1 order=(-.6 to 0 by .2) label=(a=90 'Estimated Log Hazard') minor=none;
axis2 order=(18.1 24.6 27.7 37.1) label=('Body Mass Index (kg/m^2)');
title 'Estimated Coefficients for Body Mass Index Quartiles';
proc gplot data = part4_graph;
plot p*x / vaxis = axis1 haxis = axis2;
run;
quit;
